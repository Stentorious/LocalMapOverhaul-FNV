int	iMenuID

int	iIndex
int	iDist
int	iName
int iLocked
int	iTemp
int	iBitIcon
int	iCorpse
int	iVendor
int iSize
float fPosX
float fPosY
float fPosZ
ref rIcon
ref rTemp
array_var aRefs
array_var aTemp
string_var sUIPath
string_var sTemp

begin Function {iMenuID}

	; Update player info
	SetUIFloatAlt "MapMenu\_LMO+PlrX" (Player.GetPos X)
	SetUIFloatAlt "MapMenu\_LMO+PlrY" (Player.GetPos Y)
	SetUIFloatAlt "MapMenu\_LMO+PlrZ" (Player.GetPos Z)
	SetUIFloatAlt "MapMenu\_LMO+PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetAV Perception)))
	SetUIFloatAlt "MapMenu\_LMO+Deg" (ABS (GetCellNorthRotation (Player.GPC)))

	; Get global settings
	iDist = GetUIFloatAlt "MapMenu\_LMO+Dist"
	iName = Goo1.AuxVarGetFlt "*LocalMap_INI" 3
	iBitIcon = Goo1.AuxVarGetFlt "*LocalMap_INI" 4

	; Door icon loop
	SetOnRenderUpdateEventHandler 1 (CompileScript "LocalMapOverhaul\MenuLoop.gek") 0

	; Reset icons
	UnloadUIComponent "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons"
	AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow|Map_Icons|Map_Icons"

	; Actor icons
	iTemp = Goo1.AuxVarGetFlt "*LocalMap_INI" 1
	if iTemp > 0

		; Build array of actor refs
		if iTemp == 2
			aRefs = Player.GetRefs 43 2 1 iDist
		else
			aRefs = Player.GetRefs 42 2 1 iDist
			if iTemp == 3
				aTemp = Player.GetRefs 43 2 1 iDist
				ar_Cat aRefs aTemp
			endif
		endif

		iSize = Ar_Size aRefs
		if eval iSize > 0

			; Get actor settings
			iCorpse = Goo1.AuxVarGetFlt "*LocalMap_INI" 2
			iVendor = Goo1.AuxVarGetFlt "*LocalMap_INI" 5

			; Process actor refs
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.IsActorsAIOff == 0 && rIcon.IsActorInvisibleToPlayer == 0 && rIcon.GetPlayerTeammate == 0

					; Add icon tile
					AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
					sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
					fPosX = rIcon.GetPos X
					fPosY = rIcon.GetPos Y
					fPosZ = rIcon.GetPos Z
					SetUIFloatAlt (sUIPath + "_PosX") fPosX
					SetUIFloatAlt (sUIPath + "_PosY") fPosY
					SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
					;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
					SetUIFloatAlt (sUIPath + "_DepthMod") 3
					SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
					SetUIStringAlt (sUIPath + "_Name") (rIcon.LNGetName)
					SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)
					iIndex += 1

					iTemp = rIcon.GetType

					; Display NPC name
					if eval iTemp == 42
						if iName == 0
							SetUIFloatAlt (sUIPath + "_ShowName") 0
						elseif iName == 1 || (iName == 2 && iTemp == 0)
							SetUIFloatAlt (sUIPath + "_ShowName") (rIcon.GetTalkedToPC)
						endif
					endif

					; Dead icons
					if eval rIcon.GetDead
						if eval iCorpse && rIcon.GetContainerInventoryCount == 0
							SetUIFloatAlt (sUIPath + "visible") 0
						else
							sTemp = "Interface\Stentorious\LocalMapOverhaul\ActorDead.dds"
						endif

					; ySI framework icon
					elseif eval rIcon.ySIGetTrait "tag" != ""
						sTemp = rIcon.ySIGetTrait "filename"

					; Vendor icons
					elseif eval iTemp == 42 && iVendor > 0 && rIcon.GetMerchantContainer
						rTemp = rIcon.GetClass
						if iVendor == 1
							sTemp = "interface\icons\Misc_BottleCap.dds"
						elseif eval GetServiceFlag (rIcon.GBO) 131072
							sTemp = "interface\icons\Aid_WeaponRepairKit.dds"
						elseif eval rTemp == VendorDrinks
							sTemp = "interface\icons\Aid_Alcohol.dds"
						elseif eval rTemp == Doctor
							sTemp = "interface\icons\Aid_DoctorBag.dds"
						elseif eval rTemp == VendorWeapons
							sTemp = "interface\icons\Ammo_Bullet.dds"
						elseif eval rTemp == VendorFood
							sTemp = "interface\icons\Aid_CookedMeal.dds"
						elseif eval rTemp == VendorClothes
							sTemp = "interface\icons\Apparel_Other_Bag.dds"
						elseif eval IsModLoaded "TaleOfTwoWastelands.esm" && (rTemp == (EditorIDToFormID "VendorChems") || rTemp == (EditorIDToFormID "VendorChemsFood"))
							sTemp = "interface\icons\Aid_Drug.dds"
						else
							sTemp = "interface\icons\Misc_BottleCap.dds"
						endif

					; Basic Icon
					else
						SetUIFloatAlt (sUIPath + "_Deg") (rIcon.GetAngle Z)
						SetUIFloatAlt (sUIPath + "_Hostile") (rIcon.IsCompassHostile)
						sTemp = "Interface\Stentorious\LocalMapOverhaul\Actor.dds"
					endif

					; Set icon
					SetUIStringAlt (sUIPath + "filename") (sTemp)

				endif
			loop
		endif
	endif

	; Item icons
	; Build array of item refs
	aRefs = Player.GetRefs 24 2 1 iDist	; Armor
	aTemp = Player.GetRefs 31 2 1 iDist	; Misc
	ar_Cat aRefs aTemp
	aTemp = Player.GetRefs 40 2 1 iDist	; Weapon
	ar_Cat aRefs aTemp
	aTemp = Player.GetRefs 41 2 1 iDist	; Ammo
	ar_Cat aRefs aTemp
	aTemp = Player.GetRefs 46 2 1 iDist	; Key
	ar_Cat aRefs aTemp
	aTemp = Player.GetRefs 47 2 1 iDist	; Ingestible
	ar_Cat aRefs aTemp
	aTemp = Player.GetRefs 49 2 1 iDist	; Note
	ar_Cat aRefs aTemp
	aTemp = Player.GetRefs 103 2 1 iDist; Weapon Mod
	ar_Cat aRefs aTemp
	if eval GetBit iBitIcon 0
		aTemp = Player.GetRefs 25 2 1 iDist	; Book
		ar_Cat aRefs aTemp
	endif
	if eval GetBit iBitIcon 7
		aTemp = Player.GetRefs 115 2 1 iDist; Caravan Card
		ar_Cat aRefs aTemp
	endif

	; Process item refs
	iSize = Ar_Size aRefs
	if eval iSize > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
				iTemp = rIcon.GetType
				if eval HasKeyword rIcon "LocalMapIcon" || (GetBit iBitIcon 4 && rIcon.IsQuestItem) || (GetBit iBitIcon 3 && IsItemUnique (rIcon.gbo)) || (GetBit iBitIcon 2 && iTemp == 46) || (GetBit iBitIcon 1 && iTemp == 49) || (GetBit iBitIcon 7 && iTemp == 115)

					; Add icon tile
					AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
					sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
					fPosX = rIcon.GetPos X
					fPosY = rIcon.GetPos Y
					fPosZ = rIcon.GetPos Z
					SetUIFloatAlt (sUIPath + "_PosX") fPosX
					SetUIFloatAlt (sUIPath + "_PosY") fPosY
					SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
					;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
					SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
					SetUIStringAlt (sUIPath + "_Name") (rIcon.LNGetName)
					SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)
					SetUIStringAlt (sUIPath + "filename") (rIcon.ySIGetTrait "filename")
					iIndex += 1

				endif
			endif
		loop
	endif

	; Activator icons
	; Build array of activator refs
	aRefs = Player.GetRefs 21 2 1 iDist

	; Process activator refs
	iSize = Ar_Size aRefs
	if eval iSize > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.GetDestroyed == 0 && HasKeyword rIcon "LocalMapIcon"

				; Add icon tile
				AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
				sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
				fPosX = rIcon.GetPos X
				fPosY = rIcon.GetPos Y
				fPosZ = rIcon.GetPos Z
				SetUIFloatAlt (sUIPath + "_PosX") fPosX
				SetUIFloatAlt (sUIPath + "_PosY") fPosY
				SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
				;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
				SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
				SetUIStringAlt (sUIPath + "_Name") (rIcon.LNGetName)
				SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)
				SetUIStringAlt (sUIPath + "filename") (rIcon.ySIGetTrait "filename")
				iIndex += 1

			endif
		loop
	endif

	; Projectile icons
	if eval GetBit iBitIcon 5

		; Build array of projectile refs
		aRefs = Player.GetRefs 51 2 1 iDist

		; Process projectile refs
		iSize = Ar_Size aRefs
		if eval iSize > 0
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.GetMineArmed

					; Add icon tile
					AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
					sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
					fPosX = rIcon.GetPos X
					fPosY = rIcon.GetPos Y
					fPosZ = rIcon.GetPos Z
					SetUIFloatAlt (sUIPath + "_PosX") fPosX
					SetUIFloatAlt (sUIPath + "_PosY") fPosY
					SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
					;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
					SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
					SetUIStringAlt (sUIPath + "_Name") (rIcon.LNGetName)
					SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)
					SetUIStringAlt (sUIPath + "filename") (rIcon.ySIGetTrait "filename")
					iIndex += 1

				endif
			loop
		endif
	endif

	; Lockable ref icons
	; Build array of lockable refs
	aRefs = Player.GetRefs 27 2 1 iDist	; Container
	if eval GetBit iBitIcon 8
		aTemp = Player.GetRefs 23 2 1 iDist ; Terminal
		ar_Cat aRefs aTemp
	endif
	if eval GetBit iBitIcon 9
		aTemp = Player.GetRefs 28 2 1 iDist ; Door
		ar_Cat aRefs aTemp
	endif

	; Process lockable refs
	iSize = Ar_Size aRefs
	if eval iSize > 0
		while (iSize -= 1) > -1
			rIcon = aRefs[iSize]
			if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D
				if eval (GetBit iBitIcon 6 || GetBit iBitIcon 8 || GetBit iBitIcon 9) && rIcon.GetLocked > 0 && rIcon.GetTeleportDoor == 0

					; Add icon tile
					AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
					sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
					fPosX = rIcon.GetPos X
					fPosY = rIcon.GetPos Y
					fPosZ = rIcon.GetPos Z
					SetUIFloatAlt (sUIPath + "_PosX") fPosX
					SetUIFloatAlt (sUIPath + "_PosY") fPosY
					SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
					;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
					SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
					SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)
					iIndex += 1

					; Has key
					if eval rIcon.PlayerHasKey
						SetUIStringAlt (sUIPath + "filename") "interface\icons\Misc_Keychain.dds"
						rTemp = rIcon.GetOpenKey
						SetUIStringAlt (sUIPath + "_Name") "%n%r[%n]" rIcon rTemp

					; Locked
					else
						iTemp = rIcon.GetLockLevel
						if rIcon.GetType == 27
							iTemp /= 25
						endif

						; Requires key
						iLocked = rIcon.GetLocked
						if eval iTemp > 4 || iLocked == 2
							if eval iLocked == 2
								sTemp = sv_Construct "%n%r%z" rIcon (GetStringSetting "sBroken")
							else
								sTemp = sv_Construct "%n%r[%z - %z]" rIcon (GetStringSetting "sLocked") (GetStringSetting "sLockLevelNameImpossible")
							endif
							SetUIStringAlt (sUIPath + "filename") "interface\icons\Misc_Key.dds"

						; Pickable
						else
							if iTemp == 0
								sTemp = sv_Construct "%n%r[%z - %z]" rIcon (GetStringSetting "sLocked") (GetStringSetting "sLockLevelNameVeryEasy")
							elseif iTemp == 1
								sTemp = sv_Construct "%n%r[%z - %z]" rIcon (GetStringSetting "sLocked") (GetStringSetting "sLockLevelNameEasy")
							elseif iTemp == 2
								sTemp = sv_Construct "%n%r[%z - %z]" rIcon (GetStringSetting "sLocked") (GetStringSetting "sLockLevelNameAverage")
							elseif iTemp == 3
								sTemp = sv_Construct "%n%r[%z - %z]" rIcon (GetStringSetting "sLocked") (GetStringSetting "sLockLevelNameHard")
							else
								sTemp = sv_Construct "%n%r[%z - %z]" rIcon (GetStringSetting "sLocked") (GetStringSetting "sLockLevelNameVeryHard")
							endif
							SetUIStringAlt (sUIPath + "filename") "interface\icons\Misc_BobbyPin.dds"

						endif

						SetUIStringAlt (sUIPath + "_Name") (sTemp)

					endif

					; Terminal
					if rIcon.GetType == 23
						SetUIStringAlt (sUIPath + "filename") (rIcon.ySIGetTrait "filename")
					endif

				elseif eval HasKeyword rIcon "LocalMapIcon" || rIcon.GetType == 23

					; Add icon tile
					AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
					sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
					fPosX = rIcon.GetPos X
					fPosY = rIcon.GetPos Y
					fPosZ = rIcon.GetPos Z
					SetUIFloatAlt (sUIPath + "_PosX") fPosX
					SetUIFloatAlt (sUIPath + "_PosY") fPosY
					SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
					;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
					SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
					SetUIStringAlt (sUIPath + "_Name") (rIcon.LNGetName)
					SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)
					SetUIStringAlt (sUIPath + "filename") (rIcon.ySIGetTrait "filename")
					iIndex += 1

				endif
			endif
		loop
	endif

	sv_Destruct sUIPath sTemp
	aRefs = aTemp = ar_Null

end