int	iMenuID

int	iDist
int	iName
int	iTemp
int	iCorpse
int iSize
float fPosX
float fPosY
float fPosZ
ref rIcon
array_var aRefs
array_var aTemp
string_var sUIPath
string_var sTemp

begin Function {iMenuID}

	; Update player info
	SetUIFloatAlt "MapMenu\_LMO+PlrX" (Player.GetPos X)
	SetUIFloatAlt "MapMenu\_LMO+PlrY" (Player.GetPos Y)
	SetUIFloatAlt "MapMenu\_LMO+PlrZ" (Player.GetPos Z)
	SetUIFloatAlt "MapMenu\_LMO+PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetAV Perception)))
	SetUIFloatAlt "MapMenu\_LMO+Deg" (ABS (GetCellNorthRotation (Player.GPC)))

	; Get global settings
	iDist = GetUIFloatAlt "MapMenu\_LMO+Dist"
	iName = Goo1.AuxVarGetFlt "*LocalMap_INI" 3

	; Door icon loop
	SetOnRenderUpdateEventHandler 1 (CompileScript "LocalMapOverhaul\MenuLoop.gek") 0

	; Reset icons
	UnloadUIComponent "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons"
	AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow|Map_Icons|Map_Icons"

	; Actor icons
	iTemp = Goo1.AuxVarGetFlt "*LocalMap_INI" 1
	if iTemp > 0

		; Build array of actor refs
		if iTemp == 2
			aRefs = Player.GetRefs 43 2 1 iDist
		else
			aRefs = Player.GetRefs 42 2 1 iDist
			if iTemp == 3
				aTemp = Player.GetRefs 43 2 1 iDist
				ar_Cat aRefs aTemp
			endif
		endif

		iSize = Ar_Size aRefs
		if eval iSize > 0

			; Get actor settings
			iCorpse = Goo1.AuxVarGetFlt "*LocalMap_INI" 2

			; Process actor refs
			while (iSize -= 1) > -1
				rIcon = aRefs[iSize]
				if eval IsFormValid rIcon && IsReference rIcon && rIcon.GetDisabled == 0 && rIcon.HasLoaded3D && rIcon.IsActorsAIOff == 0 && rIcon.IsActorInvisibleToPlayer == 0 && rIcon.GetPlayerTeammate == 0

					; Add icon tile
					AddTileFromTemplate "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons|Icon_Template|Icon"
					sUIPath = Sv_Construct "MapMenu\GLOW_BRANCH\MM_MainRect\MM_LocalMap_ClipWindow\Map_Icons\Icon:0\"
					rIcon.GetPosEx (fPosX) (fPosY) (fPosZ)
					SetUIFloatAlt (sUIPath + "_PosX") fPosX
					SetUIFloatAlt (sUIPath + "_PosY") fPosY
					SetUIFloatAlt (sUIPath + "_PosZ") fPosZ
					;SetUIFloatAlt (sUIPath + "_SeenData") (GetSeenDataAtPos fPosX fPosY fPosZ)
					SetUIFloatAlt (sUIPath + "_DepthMod") 3
					SetUIFloatAlt (sUIPath + "_Dist") (Player.GetDistance3D rIcon)
					SetUIStringAlt (sUIPath + "_Name") (rIcon.LNGetName)
					SetUIStringAlt (sUIPath + "_RefID") (GetFormIDString rIcon)

					iTemp = rIcon.GetType

					; Display NPC name
					if eval iTemp == 42
						if iName == 0
							SetUIFloatAlt (sUIPath + "_ShowName") 0
						elseif iName == 1 || (iName == 2 && iTemp == 0)
							SetUIFloatAlt (sUIPath + "_ShowName") (rIcon.GetTalkedToPC)
						endif
					endif

					; Dead icons
					if eval rIcon.GetDead
						if eval iCorpse && rIcon.GetContainerInventoryCount == 0
							SetUIFloatAlt (sUIPath + "visible") 0
						else
							sTemp = "Interface\Stentorious\LocalMapOverhaul\ActorDead.dds"
						endif

					; Basic Icon
					else
						SetUIFloatAlt (sUIPath + "_Deg") (rIcon.GetAngle Z)
						SetUIFloatAlt (sUIPath + "_Hostile") (rIcon.IsCompassHostile)
						sTemp = "Interface\Stentorious\LocalMapOverhaul\Actor.dds"
					endif

					; Set icon
					SetUIStringAlt (sUIPath + "filename") (sTemp)

				endif
			loop
		endif
	endif

	sv_Destruct sUIPath sTemp
	aRefs = aTemp = ar_Null

end