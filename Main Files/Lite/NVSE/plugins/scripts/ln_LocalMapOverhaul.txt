; Local Map Overhaul Lite 1.5 - Stentorious

; On game restart
if Goo1.AuxVarGetFlt "*LocalMap_Init" != 1
	Goo1.AuxVarSetFlt "*LocalMap_Init" 1 0

	; Check for requirements
	if GetNVSEVersion > 5 && GetNVSERevision > 2 && GetNVSEBeta > 2
	else
		MessageBoxEx "Local Map Overhaul missing requirement!%rInstall xNVSE 6.3.3+."
		return
	endif
	if GetPluginVersion "JohnnyGuitarNVSE" < 390
		MessageBoxEx "Local Map Overhaul missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
		return
	endif
	if GetPluginVersion "ShowOffNVSE Plugin" < 180
		MessageBoxEx "Local Map Overhaul missing requirement!%rInstall latest ShowOff NVSE plugin."
		return
	endif
	if GetPluginVersion "lStewieAl's Tweaks" < 930
		MessageBoxEx "Local Map Overhaul missing requirement!%rInstall latest lStewieAl's Tweaks."
		return
	endif
	if GetPluginVersion "UI Organizer Plugin" < 230
		MessageBoxEx "Local Map Overhaul missing requirement!%rInstall latest User Interface Organizer plugin."
		return
	endif
	Goo1.AuxVarSetFlt "*LocalMap_Init" 1 1

	; Load interface settings
	SetUIFloatAlt "MapMenu\_LMO+CropW" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "Interface:fIconCropWorld" "Stentorious\LocalMapOverhaul.ini")))
	SetUIFloatAlt "MapMenu\_LMO+SizeL" (GetMaxOf 0.75 (GetMinOf 1.5 (GetINIFloat "Interface:fIconSizeLocal" "Stentorious\LocalMapOverhaul.ini")))
	SetUIFloatAlt "MapMenu\_LMO+SizeW" (GetMaxOf 0.5 (GetMinOf 2 (GetINIFloat "Interface:fIconSizeWorld" "Stentorious\LocalMapOverhaul.ini")))
	SetUIFloatAlt "MapMenu\_LMO+DropL" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "Interface:bDropShadowLocal" "Stentorious\LocalMapOverhaul.ini")))
	SetUIFloatAlt "MapMenu\_LMO+DropW" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "Interface:bDropShadowWorld" "Stentorious\LocalMapOverhaul.ini")))
	SetNumericGameSetting "fSeenDataUpdateRadius" ((GetMaxOf 5 (GetMinOf 100 (GetINIFloat "Interface:fFogOfWarRadius" "Stentorious\LocalMapOverhaul.ini"))) * 69.99104)

	; Global icon settings
	SetUIFloatAlt "MapMenu\_LMO+SeenData" (GetMaxOf 0 (GetMinOf 1 (GetINIFloat "Icons:bRespectFogOfWar" "Stentorious\LocalMapOverhaul.ini")))
	SetUIFloatAlt "MapMenu\_LMO+Dist" ((GetMaxOf 5 (GetMinOf 100 (GetINIFloat "Icons:fIconRadius" "Stentorious\LocalMapOverhaul.ini"))) * 69.99104)
	if GetINIFloat "Icons:bPerceptionCalc" "Stentorious\LocalMapOverhaul.ini" == 1
		SetUIFloatAlt "MapMenu\_LMO+DistMode" 1
		SetOnActorValueChangeEventHandler 1 ({int iAVCode, float fOldValue, float fNewValue} => SetUIFloatAlt "MapMenu\_LMO+PlrAV6" (GetMaxOf 1 (GetMinOf 10 (Player.GetActorValue Perception)))) 0 6
	endif
	if GetINIFloat "Icons:bHeightIndicator" "Stentorious\LocalMapOverhaul.ini" == 1
		SetUIFloatAlt "MapMenu\_LMO+Height" 1
	endif
	if GetINIFloat "Icons:bFadeAlpha" "Stentorious\LocalMapOverhaul.ini" == 1
		SetUIFloatAlt "MapMenu\_LMO+Fade" 1
	endif

	; Actor settings
	SetUIFloatAlt "MapMenu\_LMO+Color0" 4
	if GetINIFloat "Actors:bHostileColor" "Stentorious\LocalMapOverhaul.ini" == 1
		SetUIFloatAlt "MapMenu\_LMO+Color2" 2
	endif
	Goo1.AuxVarSetFlt "*LocalMap_INI" 0 0
	Goo1.AuxVarSetFlt "*LocalMap_INI" (GetMaxOf 0 (GetMinOf 3 (GetINIFloat "Actors:iNameVisibility" "Stentorious\LocalMapOverhaul.ini"))) 1
	Goo1.AuxVarSetFlt "*LocalMap_INI" (GetMaxOf 0 (GetMinOf 3 (GetINIFloat "Actors:iMode" "Stentorious\LocalMapOverhaul.ini"))) 2
	Goo1.AuxVarSetFlt "*LocalMap_INI" (GetINIFloat "Actors:bHideEmptyCorpses" "Stentorious\LocalMapOverhaul.ini") 3

endif

; On game load
if Goo1.AuxVarGetFlt "*LocalMap_Init" 1 == 1
	SetOnMenuOpenEventHandler (CompileScript "LocalMapOverhaul\OnMenuOpen1023.gek") 1 1023
	SetOnMouseoverChangeEventHandler (CompileScript "LocalMapOverhaul\OnMouseover1023.gek") 1 1023
	SetOnMenuClickEventHandler (CompileScript "LocalMapOverhaul\OnIconClick.gek") 1 "MapMenu#50"
	SetOnMenuCloseEventHandler ({int iMenuID} => SetJohnnyOnRenderUpdateEventHandler 0 (CompileScript "LocalMapOverhaul\MenuLoop.gek")) 1 1023
	SetJohnnyOnRenderUpdateEventHandler 0 (CompileScript "LocalMapOverhaul\MenuLoop.gek")
endif